# Config file for the @PROJECT_NAME@ package
# Defines the following variables:
#
#  @PROJECT_NAME@_FEATURES       - list of enabled features
#  @PROJECT_NAME@_VERSION        - version of the package
#  @PROJECT_NAME@_GIT_SHA1       - Git revision of the package
#  @PROJECT_NAME@_GIT_SHA1_SHORT - short Git revision of the package
#
### compute paths

get_filename_component(@PROJECT_NAME@_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

if( DEFINED ECBUILD_2_COMPAT AND ECBUILD_2_COMPAT )

  set( @PNAME@_SELF_INCLUDE_DIRS "@CONF_INCLUDE_DIRS@" )
  set( @PNAME@_SELF_DEFINITIONS  "@CONF_DEFINITIONS@" )
  set( @PNAME@_SELF_LIBRARIES    "@CONF_LIBRARIES@" )

  set( @PNAME@_TPLS              "@CONF_TPLS@" )
  set( @PNAME@_TPL_INCLUDE_DIRS  "@CONF_TPL_INCLUDE_DIRS@" )
  set( @PNAME@_TPL_DEFINITIONS   "@CONF_TPL_DEFINITIONS@" )
  set( @PNAME@_TPL_LIBRARIES     "@CONF_TPL_LIBRARIES@" )

  set( @PNAME@_VERSION           "@PACKAGE_VERSION@" )
  set( @PNAME@_GIT_SHA1          "@PACKAGE_GIT_SHA1@" )
  set( @PNAME@_GIT_SHA1_SHORT    "@PACKAGE_GIT_SHA1_SHORT@" )

  ### export include paths as absolute paths

  set( @PNAME@_INCLUDE_DIRS "" )
  foreach( path ${@PNAME@_SELF_INCLUDE_DIRS} )
    get_filename_component( abspath ${path} ABSOLUTE )
    list( APPEND @PNAME@_INCLUDE_DIRS ${abspath} )
  endforeach()
  list( APPEND @PNAME@_INCLUDE_DIRS ${@PNAME@_TPL_INCLUDE_DIRS} )

  ### export definitions

  set( @PNAME@_DEFINITIONS      ${@PNAME@_SELF_DEFINITIONS} ${@PNAME@_TPL_DEFINITIONS} )

  ### export list of all libraries

  set( @PNAME@_LIBRARIES        ${@PNAME@_SELF_LIBRARIES}   ${@PNAME@_TPL_LIBRARIES}   )

endif()

set( @PROJECT_NAME@_VERSION           "@PACKAGE_VERSION@" )
set( @PROJECT_NAME@_GIT_SHA1          "@PACKAGE_GIT_SHA1@" )
set( @PROJECT_NAME@_GIT_SHA1_SHORT    "@PACKAGE_GIT_SHA1_SHORT@" )

### export the features provided by the package

set( @PROJECT_NAME@_FEATURES    "@CONF_FEATURES@" )
if( DEFINED ECBUILD_2_COMPAT AND ECBUILD_2_COMPAT )
  set( @PNAME@_FEATURES ${@PROJECT_NAME@_FEATURES} )
endif()
foreach( _f ${@PROJECT_NAME@_FEATURES} )
  set( @PROJECT_NAME@_HAVE_${_f} 1 )
  if( DEFINED ECBUILD_2_COMPAT AND ECBUILD_2_COMPAT )
    set( @PNAME@_HAVE_${_f} 1 )
  endif()
endforeach()

# Has this configuration been exported from a build tree?
set( @PROJECT_NAME@_IS_BUILD_DIR_EXPORT @_is_build_dir_export@ )

if( EXISTS ${@PROJECT_NAME@_CMAKE_DIR}/@CONF_IMPORT_FILE@ )
  set( @PROJECT_NAME@_IMPORT_FILE "${@PROJECT_NAME@_CMAKE_DIR}/@CONF_IMPORT_FILE@" )
  include( ${@PROJECT_NAME@_IMPORT_FILE} )
endif()

# here goes the imports of the TPL's
if( DEFINED ECBUILD_2_COMPAT AND ECBUILD_2_COMPAT )
  include( ${CMAKE_CURRENT_LIST_FILE}.tpls OPTIONAL )
endif()

# insert definitions for IMPORTED targets

if( NOT @PROJECT_NAME@_BINARY_DIR )

  if( @PROJECT_NAME@_IS_BUILD_DIR_EXPORT )
    include( "@TOP_PROJECT_TARGETS_FILE@" OPTIONAL )
  else()
    include( "${@PROJECT_NAME@_CMAKE_DIR}/@PROJECT_NAME@-targets.cmake" OPTIONAL )
  endif()

endif()

# publish this file as imported

set( @PROJECT_NAME@_IMPORT_FILE ${CMAKE_CURRENT_LIST_FILE} )
mark_as_advanced( @PROJECT_NAME@_IMPORT_FILE )
if( DEFINED ECBUILD_2_COMPAT AND ECBUILD_2_COMPAT )
  set( @PNAME@_IMPORT_FILE ${CMAKE_CURRENT_LIST_FILE} )
  mark_as_advanced( @PNAME@_IMPORT_FILE )
endif()

# set @PROJECT_NAME@_BASE_DIR for final installations or build directories

if( NOT @PROJECT_NAME@ )
  if( @PROJECT_NAME@_IS_BUILD_DIR_EXPORT )
    set( @PROJECT_NAME@_BASE_DIR @CMAKE_BINARY_DIR@ )
  else()
    get_filename_component( abspath ${CMAKE_CURRENT_LIST_DIR}/../../.. ABSOLUTE )
    set( @PROJECT_NAME@_BASE_DIR ${abspath} )
  endif()
  if( DEFINED ECBUILD_2_COMPAT AND ECBUILD_2_COMPAT )
    set( @PNAME@_BASE_DIR ${@PROJECT_NAME@_BASE_DIR} )
  endif()
endif()
